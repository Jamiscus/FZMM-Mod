plugins {
    id 'fabric-loom' version '1.8-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = "${project.archives_base_name}-mc${project.minecraft_version}"
}

repositories {
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/releases/"
    }

    maven { url 'https://maven.wispforest.io' }

    maven { url = "https://api.modrinth.com/maven" }

}

loom {
    accessWidenerPath = file("src/main/resources/fzmm.accesswidener")
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Dependencies
    // Fabric API
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    def fabricDependencies = [
            'fabric-command-api-v2',
            'fabric-item-group-api-v1',
            'fabric-key-binding-api-v1',
            'fabric-resource-loader-v0',
            'fabric-object-builder-api-v1',
            'fabric-renderer-api-v1'
    ]

    fabricDependencies.forEach {
        modImplementation(fabricApi.module(it, project.fabric_version))
    }

    // owo lib
    modImplementation annotationProcessor ("io.wispforest:owo-lib:${project.owo_version}")

    // include this if you don't want force your users to install owo
    // sentinel will warn them and give the option to download it automatically
    include "io.wispforest:owo-sentinel:${project.owo_version}"

    // Mod Menu
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

    // PlacerholderAPI
    modImplementation(include("maven.modrinth:placeholder-api:${project.placeholder_api_version}"))

    // Optional dependencies
    // Symbol chat
    modImplementation "maven.modrinth:symbol-chat:${project.symbol_chat_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {

    }
}